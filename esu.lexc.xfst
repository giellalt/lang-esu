! LANGUAGE: "esu"
! AUTHOR: "Lonny Strunk"

! define MultipleForms
    ! for @~+lu/@na (ch) and others with \w/\w !!!!TODO!!!!
    ! for [Opt_PRS_2Sg] (ch5 pg67)
    ! for [Opt_PRS_2Sg_3Sg]
    ! for [Opt_PRS_2Sg_1Sg]
! change the multi-char symbols to single char??
! change capital to lowercase
! give to twolc

define Stop               [ p | t | c | k | q ];
define VoicedFricative    [ v | l | s | g | r | u͡g | u͡r ];
define VoicelessFricative [ vv | ll | ss | gg | rr | u͡gg | u͡rr ];
define VoicedNasal        [ m | n | ng ];
define VoicelessNasal     [ ḿ | ń | ńg ];

define VoicedC    [ VoicedFricative | VoicedNasal ];
define VoicelessC [ VoicelessFricative | VoicelessNasal | Stop ];

define Fricative  [ VoicelessFricative | VoicedFricative ];
define Nasal      [ VoicelessNasal     | VoicedNasal     ];

define Con        [ Stop | Fricative | Nasal | w | y ];

define FullVowel   [ a | i | u ];
define Vow         [ e | FullVowel ];

define Lowercase "A"   -> a,
                 "C"   -> c,
                 "E"   -> e,
                 "G"   -> g,
                 "I"   -> i,
                 "K"   -> k,
                 "L"   -> l,
                 "M"   -> m,
                 "N"   -> n,
                 "P"   -> p,
                 "Q"   -> q,
                 "R"   -> r,
                 "S"   -> s,
                 "T"   -> t,
                 "U"   -> u, 
                 "V"   -> v,
                 "W"   -> w,
                 "Y"   -> y,
                 "Ḿ"   -> ḿ,
                 "Ń"   -> ń,
                 "Vv"  -> vv,
                 "Ll"  -> ll,
                 "Ss"  -> ss,
                 "Gg"  -> gg,
                 "Rr"  -> rr,
                 "Ng"  -> ng,
                 "Ńg"  -> ńg,
                 "U͡g"  -> u͡g,
                 "U͡r"  -> u͡r,
                 "U͡gg" -> u͡gg,
                 "U͡rr" -> u͡rr;

define MultipleForms "[Opt_PRS_2Sg]" -> "~" "(g)" i         || [ Vow Vow | [ Con - t ] e ] _  .o.
                     "[Opt_PRS_2Sg]" -> "+" n               || t e  _                         .o.
                     t e "*" "[Opt_PRS_2Sg]" -> l u                                           .o.
                     "[Opt_PRS_2Sg]" -> ":" a               || Con  _                         .o.
                     "[Opt_PRS_2Sg]" -> 0                                                     .o.
                     "[Opt_PRS_2Sg_3Sg]" -> "~" "(g)" i u   || [ Vow Vow | [ Con - t ] e ] _  .o.
                     t e "[Opt_PRS_2Sg_3Sg]" -> s g u                                         .o.
                     t e "*" "[Opt_PRS_2Sg_3Sg]" -> l g u                                     .o.
                     "[Opt_PRS_2Sg_3Sg]" -> "–" gg u        || g _                            .o.
                     "[Opt_PRS_2Sg_3Sg]" -> "–" rr u        || r _                            .o.
                     "[Opt_PRS_2Sg_3Sg]" -> u                                                 .o.
                     "[Opt_PRS_2Sg_1Sg]" (->) "~" "(g)" i a || [ Vow Vow | [ Con - t ] e ] _  .o.
                     "[Opt_PRS_2Sg_1Sg]" -> "@" "+" ng a                                      .o.
                     "@~+lu/@na" -> "@" n a                 || t e "*" _                      .o.
                     "@~+lu/@na" -> "@" "~" "+" l u                                            ;


regex Lowercase .o. MultipleForms ; 
