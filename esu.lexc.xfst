! LANGUAGE: "esu"
! AUTHOR: "Lonny Strunk"

! define MultipleForms
    ! for [Opt_PRS_2Sg_1Sg]
    ! multi-form inflections .vet/+pet ...
    ! for lria/lrii/ngur*
    ! for nru/llru
    ! 
! change the multi-char symbols to single char??
! change capital to lowercase
! give to twolc

define Stop       [ p | t | c | k | q ];
define VoiceFric  [ v | l | s | g | r | u͡g | u͡r ];
define VlessFric  [ vv | ll | ss | gg | rr | u͡gg | u͡rr ];
define VoiceNasal [ m | n | ng ];
define VlessNasal [ ḿ | ń | ńg ];

define VoiceCon   [ VoiceFric | VoiceNasal ];
define VlessCon   [ VlessFric | VlessNasal | Stop ];

define Fric       [ VlessFric  | VoiceFric  ];
define Nasal      [ VlessNasal | VoiceNasal ];

define Con        [ Stop | Fric | Nasal | w | y ];

define FVow       [ a | i | u ];
define Vow        [ e | FVow ];

define Lowercase "A"   -> a,
                 "C"   -> c,
                 "E"   -> e,
                 "G"   -> g,
                 "I"   -> i,
                 "K"   -> k,
                 "L"   -> l,
                 "M"   -> m,
                 "N"   -> n,
                 "P"   -> p,
                 "Q"   -> q,
                 "R"   -> r,
                 "S"   -> s,
                 "T"   -> t,
                 "U"   -> u,
                 "V"   -> v,
                 "W"   -> w,
                 "Y"   -> y,
                 "Ḿ"   -> ḿ,
                 "Ń"   -> ń,
                 "Vv"  -> vv,
                 "Ll"  -> ll,
                 "Ss"  -> ss,
                 "Gg"  -> gg,
                 "Rr"  -> rr,
                 "Ng"  -> ng,
                 "Ńg"  -> ńg,
                 "U͡g"  -> u͡g,
                 "U͡r"  -> u͡r,
                 "U͡gg" -> u͡gg,
                 "U͡rr" -> u͡rr;

define AllomorphDecouple "(g/t)" -> "(g)" "(t)" .o. 
                         "(r/l)" -> "(r)" "(l)" .o. 
                         "(u/i)" -> "(u)" "(i)" .o.
                         "(ar)"  -> a r "*"     .o.
                         "(ar*)" -> a r "*"      ; 

define MultipleForms "[Opt_PRS_2Sg]" -> "~" "(g)" i         || [ Vow Vow | [ Con - t ] e ] _  .o.
                     "[Opt_PRS_2Sg]" -> "+" n               || t e  _                         .o.
                     t e "*" "[Opt_PRS_2Sg]" -> l u                                           .o.
                     "[Opt_PRS_2Sg]" -> ":" a               || Con  _                         .o.
                     "[Opt_PRS_2Sg]" -> 0                                                     .o.
                     "[Opt_PRS_2Sg_3Sg]" -> "~" "(g)" i u   || [ Vow Vow | [ Con - t ] e ] _  .o.
                     t e "[Opt_PRS_2Sg_3Sg]" -> s g u                                         .o.
                     t e "*" "[Opt_PRS_2Sg_3Sg]" -> l g u                                     .o.
                     "[Opt_PRS_2Sg_3Sg]" -> "–" gg u        || g _                            .o.
                     "[Opt_PRS_2Sg_3Sg]" -> "–" rr u        || r _                            .o.
                     "[Opt_PRS_2Sg_3Sg]" -> u                                                 .o.
                     "[Opt_PRS_2Sg_1Sg]" (->) "~" "(g)" i a || [ Vow Vow | [ Con - t ] e ] _  .o.
                     "[Opt_PRS_2Sg_1Sg]" -> "@" "+" ng a                                      .o.
                     "@~+lu/@na" -> "@" n a                 || t e "*" _                      .o.
                     "@~+lu/@na" -> "@" "~" "+" l u                                           .o.
                     "@+p/~v" -> "@" "+" p                  || [ t e ("*") | Con ] _          .o.
                     "@+p/~v" -> "~" v                                                        .o.
!                     "(ʼa)" -> ʼ a                          || Con a _                        .o.
!                     "(ʼa)" -> 0                                                              .o.
                     "–rpag/@vag" (->) "@" v a g            || Vow t e _                      .o.
                     "–rpag/@vag"  ->  "–" r p a g                                            .o.
                     "+ciqe/@ciiqe" -> "@" c i i q e        || t e ("*") _                    .o. 
                     "+ciqe/@ciiqe" -> "+" c i q e                                            .o. 
                     "@+cete/.vkar" -> "@" "+" c e t e      || Con _                          .o.
                     "@+cete/.vkar" (->) "@" "+" c e t e    || t e ("*") _                    .o.
                     "@+cete/.vkar" -> "." v k a r                                            .o.
                     "+c/.ss" -> "." ss                     || Vow _                          .o.
                     "+c/.ss" -> "+" c                      ;


define AutomaticDevoicing v -> vv,
                          l -> ll,
                          s -> ss,
                          g -> gg,
                          r -> rr,
                          u͡g -> u͡gg,
                          u͡r -> u͡rr || [ Stop | VlessFric ] _  ,
                                       _ [ Stop ]                      .o.
                          m -> ḿ,
                          n -> ń,
                          ng -> ńg || [ Stop | VlessFric ] _   ;
!                          r -> rr  || _ .#.                            .o.
!                          s -> ss  || .#. _                             ;

regex Lowercase          .o.
      AllomorphDecouple  .o.
      MultipleForms      .o.  
      AutomaticDevoicing  ;
