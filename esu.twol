Alphabet
    a c e g i k l m n p q r s t u v w y ḿ ń
    vv ll ss gg rr ng ńg u͡g u͡gg u͡r u͡rr
    %[e%]:e %[e%]:0 
    
    %(a%):%> %(e%):%> %(g%):%> %(r%):%> %(s%):%> %(t%):%> %(u%):%> 
    %(aa%):%> %(ar%):%> %(ar%*%):%> %(ng%):%> %(ur%):%>
    %(g%/t%):%> %(r%/l%):%> %(u%/i%):%>
    
    %+:%> %–:%> %~:%> %%:%> %::%> %':%> %.:%> %@:%> %*:%>
    %= ; 

Sets 
    Bndry = %+ %– %~ %% %: %' %. %@ %* %(a%) %(e%) %(g%) %(r%) %(s%) %(t%) %(u%) %(aa%) %(ar%) %(ar%*%) %(ng%) %(ur%) %(g%/t%) %(r%/l%) %(u%/i%) ;

    Stop = p t c k q ;
    VoiceFric = v l s g r u͡g u͡r ; 
    VlessFric = vv ll ss gg rr u͡gg u͡rr ;
    VoiceNasal = m n ng ;
    VlessNasal = ḿ ń ńg ;

    VoicedCns = VoiceFric VoiceNasal ;
    VlessCns = VlessFric VlessNasal ;

    Fric = VoiceFric VlessFric ;
    Nasal = VoiceNasal VlessNasal ;

    Cns = Stop Fric Nasal w y ;

    FVow = a i u ;
    Vow = FVow e ;

Rules 

! uita+'(g/t)uq
! uita<0 0   uq
"Only one boundary"
Bndry:0 <=> Bndry:%> _ ;

! aqume+'(g/t)uq
! aqum0<0 0   uq
!"vowel postbases are e deleters"
!e:0 <=> _ Bndry:+ Vow: ;
!e:0 <=> _ [ Bndry: - %~: ]+ Vow: ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!     OPERATORS     !!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! nere+'(g/t)uq
! nerʼ<0 0   uq
"Rule for Apostrophe - gemination of short bases"
!e:%ʼ <=> .#. (Cns) Vow Cns _ Bndry:* %': ;
e:%ʼ <=> .#. (Cns) Vow Cns _ [ Bndry: - %~: ]* %': ;


"Rule for Tilde - e deletion"
!e:0 <=> _ Bndry:* %~: ;
e:0 <=> _ [ Bndry: - %': ]* %~: ;
        _ [ Bndry: - %~: ]+ Vow ;
    except
        .#. (Cns) Vow Cns _ [ Bndry: - %~: ]* %': ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!     ALLOPHONES    !!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! kaig+'(g/t)uq
! kaig<0 t   uq
"Rule (t):t - consonant ending base"
![ %(g%/t%):t | %(t%):t ] <=> .#. [[ :* Cns Bndry:* ] - [ :* :Vow Bndry:* :Vow Bndry:* ]] _ ;
[ %(g%/t%):t | %(t%):t ] <=> Cns Bndry:* _ ;
                         except
                             :Vow Bndry:* :Vow Bndry:* _ ;


! qia+'(g/t)uq
! qia<0 g   uq
"Rule (g):g - double vowel ending base"
[ %(g%/t%):g | %(g%):g ] <=> :Vow Bndry:* :Vow Bndry:* _ ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!     WORD FINAL    !!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

"Word final Vte -> t:n"
t:n <=> Vow _ e:0 [ .#. | %= ] ;

"Word final Vte -> e:0"
e:0 <=> Vow t:n _ [ .#. | %= ] ; 

"Word final e:a"
e:a <=> .#. ~[ :* Vow t:n] _ [ .#. | %= ] ; 

"Word Final changes - g:k, r:q"
[ g:k | r:q ] <=> _ Bndry:* [ .#. | %= ] ;


! evcug[V][Intr][Ind][2Pl]=llu[Encl]:evcug+'(g/t)uci=llu
