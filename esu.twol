! LANGUAGE: "esu"
! AUTHOR: "Lonny Strunk"

Alphabet
    a c e g i k l m n p q r s t u v w y ḿ ń
    vv ll ss gg rr ng ńg u͡g u͡gg u͡r u͡rr
    %[e%]:e %[e%]:0 
    
    %(a%):%> %(e%):%> %(g%):%> %(r%):%> %(s%):%> %(t%):%> %(u%):%> 
    %(aa%):%> %(ar%):%> %(ar%*%):%> %(ng%):%> %(ur%):%>
    %(g%/t%):%> %(r%/l%):%> %(u%/i%):%>
    
    %+:%> %–:%> %~:%> %%:%> %::%> %':%> %.:%> %@:%> %*:%>
    %= ; 

Sets 
    Bndry = %+ %– %~ %% %: %' %. %@ %* %(a%) %(e%) %(g%) %(r%) %(s%) %(t%) %(u%) %(aa%) %(ar%) %(ar%*%) %(ng%) %(ur%) %(g%/t%) %(r%/l%) %(u%/i%) ;

    Stop = p t c k q ;
    VoiceFric = v l s g r u͡g u͡r ; 
    VlessFric = vv ll ss gg rr u͡gg u͡rr ;
    VoiceNasal = m n ng ;
    VlessNasal = ḿ ń ńg ;

    VoicedCns = VoiceFric VoiceNasal ;
    VlessCns = VlessFric VlessNasal Stop ;

    Fric = VoiceFric VlessFric ;
    Nasal = VoiceNasal VlessNasal ;

    Cns = Stop Fric Nasal w y ;

    FVow = a i u ;
    Vow = FVow e ;

Rules 

!"a:0"
!a:0 <=> _ Cns:0 Bndry:* %–: %–: l:0 ; ! "Rule for Double Minus" ! - optional drop preceding vowel also
!    except
!        _ g:0 Bndry:* %(e%):i ; ! age -> ii 
!        _ g:0 Bndry:* a:i ; ! aga -> ii 
!        _ Bndry:* [ g:0 | %(ng%):0 ] Bndry:* a:i ; ! anga, aga -> ii
!        a:i Bndry:* [ g:0 | %(ng%):0 ] Bndry:* _ ; ! anga, aga -> ii
!        e:i Bndry:* %(ng%):0 Bndry:* _ ; ! enga -> ii

"a:i"
a:i <=> _ g:0 Bndry:* %(e%):i ; ! age -> ii 
        _ g:0 Bndry:* a:i ; ! aga -> ii 
        _ Bndry:* [ g:0 | %(ng%):0 ] Bndry:* a:i ; ! anga, aga -> ii
        a:i Bndry:* [ g:0 | %(ng%):0 ] Bndry:* _ ; ! anga, aga -> ii
        e:i Bndry:* %(ng%):0 Bndry:* _ ; ! enga -> ii

"e:0"
e:0 <=> _ [ Bndry: - %': ]* %~: ; ! "Rule for Tilde"
        Vow t:n _ [ .#. | %= ] ; ! "Word final Vte -> e:0"
        _ [ Bndry: - %~: ]+ Vow ; ! "vowel postbases are e deleters"
        _ [ r | g ] Bndry:* %%:0 Bndry:* %(e%):e ; ! "Rule (e):e"
    except
        .#. (Cns) Vow Cns _ [ Bndry: - %~: ]* %': ; ! "Rule for Apostrophe"
        _ Bndry:* %(ng%):0 Bndry:* a:i ; ! enga -> ii
        _ Bndry:* %(ng%):0 Bndry:* i ; ! engi -> ai

"e:a"
e:a <=> .#. ~[ :* Vow t:n] _ [ .#. | %= ] ; ! "Word final e:a"
        _ Bndry:* %(ng%):0 Bndry:* i ; ! engi -> ai
    except
        .#. (Cns) Vow Cns _ [ Bndry: - %~: ]* %': ; ! "Rule for Apostrophe - short bases"

"e:i"
e:i <=> _ Bndry:* %(ng%):0 Bndry:* a:i ; ! enga -> ii
    except
        .#. (Cns) Vow Cns _ [ Bndry: - %~: ]* %': ; ! "Rule for Apostrophe - short bases"

"e:%ʼ"
e:%ʼ <=> .#. (Cns) Vow Cns _ [ Bndry: - %~: ]* %': ; ! "Rule for Apostrophe - short bases"

"g:0"
g:0 <=> _ Bndry:* %–: ; ! Rule for Minus - drop consonant ending base
        \Vow Bndry:* :FVow Bndry:* _ Bndry:* %:: Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon
        \Vow Bndry:* :FVow Bndry:* %:: Bndry:* _ Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon

"g:k"
g:k <=> _ (%*:) [ .#. | %= ] ; ! Word Final changes

!"i:0"
!i:0 <=> _ Cns:0 Bndry:* %–: %–: l:0 ; ! "Rule for Double Minus" ! - optional drop preceding vowel also
!
!"l drop" ! - optional drop preceding vowel also
!l:0 => Vow:0 Cns:0 Bndry:* %–: %–: _ ; ! Rule for Double Minus - 

"ng:0"
ng:0 <=> \Vow Bndry:* :FVow Bndry:* _ Bndry:* %:: Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon
         \Vow Bndry:* :FVow Bndry:* %:: Bndry:* _ Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon

"r:0"
r:0 <=> _ Bndry:* %–: ; ! Rule for Minus - drop consonant ending base
        FVow _ [Bndry: - %*:]* %%:%> ; ! Rule for Percent
        \Vow Bndry:* :FVow Bndry:* _ Bndry:* %:: Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon
        \Vow Bndry:* :FVow Bndry:* %:: Bndry:* _ Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon

"r:q"
r:q <=> _ (%*:) [ .#. | %= ] ; ! Word Final changes

"rr:0"
rr:0 <=> _ Bndry:* %–: ; ! Rule for Minus - drop consonant ending base

!"ti to ci" ! - optional drop preceding vowel also
!t:c <=> _ Vow:0 Cns:0 Bndry:* %–: %–: l:0 ; ! Rule for Double Minus -

"Word final Vte -> t:n"
t:n <=> Vow _ e:0 [ .#. | %= ] ;

!"u:0"
!u:0 <=> _ Cns:0 Bndry:* %–: %–: l:0 ; ! "Rule for Double Minus" ! - optional drop preceding vowel also

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!     ALLOPHONES    !!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

"Rule (e):e" ! - bases kept with percent
%(e%):e <=> FVow FVow [ r | g ] Bndry:* %%:0 Bndry:* _ ;
                  e:0 [ r | g ] Bndry:* %%:0 Bndry:* _ ;

"(e):i"
%(e%):i <=> a:i g:0 Bndry:* %%:0 Bndry:* _ ; ! age -> ii 

"Rule (e):FV" ! - bases kept with percent and dropped by colon
%(e%):Vy <=> \Vow Vx [ g:0 | r:0 ] Bndry:* %%:0 Bndry:* %:: Bndry:* _ Bndry:* \Vow ;
    where Vx in ( a i u )
          Vy in ( a i u )
    matched ;

! kaig+'(g/t)uq
! kaig<0 t   uq
"Rule (t):t" ! - consonant ending base
Tx:t <=> Cns Bndry:* _ ;
    except
        :Vow [:%>|:0]* :Vow [:%>|:0]* _ ; ! "Rule (g):g"
    where Tx in ( %(g%/t%) %(t%) ) ;

! qia+'(g/t)uq
! qia<0 g   uq
"Rule (g):g" ! - double vowel ending base
Gx:g <=> :Vow [:%>|:0]* :Vow [:%>|:0]* _ ;
    where
        Gx in ( %(g%/t%) %(g%) ) ;


"Rule (ng):ng" ! - bases ending in a vowel
%(ng%):ng <=> Vow: Bndry:* _ ;
    except
        \Vow Bndry:* :FVow Bndry:* _ Bndry:* %:: Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon
        \Vow Bndry:* :FVow Bndry:* %:: Bndry:* _ Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon

"Rule (ng):0" ! - bases that get added then dropped after single vowel
%(ng%):0 <=> \Vow Bndry:* :FVow Bndry:* _ Bndry:* %:: Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon
             \Vow Bndry:* :FVow Bndry:* %:: Bndry:* _ Bndry:* :FVow Bndry:* \Vow ; ! Rule for Colon

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!     OPERATOR      !!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

"%:0 instead" ! percent not used
%%:0 <=> g: Bndry:* _ ;
         e: r: Bndry:* _ ;
         r: %*: Bndry:* _ ;


! aqume+'(g/t)uq
! aqum0<0 0   uq
!"vowel postbases are e deleters"

! "Rule for Plus" - useless
! "Rule for Period" - useless

! nere+'(g/t)uq
! nerʼ<0 0   uq
! "Rule for Apostrophe" ! - gemination of short bases 

! assir-–llru[V→V][V][Intr][Ind][3Sg]
! assir–llru+'(g/t)uq
! assi0<llru<0 0   uq
! assillruuq
!"Rule for Minus" ! - drop consonant ending base

! arnar[N][Abl_Mod][Unpd][Sg]
! arnar%~mek
! arna0<0mek
! arnamek
!"Rule for Percent" ! - drop weak r ending base

! a n g y a r %% %: %(e%) t
! a n g y a 0 0  0  0     t

! a t k u g %% %: %(e%) t
! a t k u 0 0  0  u     t 

! kaig+'(g/t)uq
! kaig<0 t   uq
! "Rule (t):t" ! - consonant ending base

! ! qia+'(g/t)uq
! ! qia<0 g   uq
! "Rule (g):g" ! - double vowel ending base

! evcug[V][Intr][Ind][2Pl]=llu[Encl]:evcug+'(g/t)uci=llu
