! LANGUAGE: "esu"
! AUTHOR: "Lonny Strunk"

Alphabet
    a c e g i k l m n p q r s t u v w y ḿ ń
    vv ll ss gg rr ng ńg u͡g u͡gg u͡r u͡rr
    %[e%] %[e%]:e                               ! HBC/Nun: initial e is not weak
    ʼ                                           ! Keep apostrophe from lexical entries
    
    ! All operators and allomorph operators map to morpheme segmenter 
    %(a%):%> %(e%):%> %(g%):%> %(i%):%> %(l%):%> %(r%):%> %(s%):%> %(t%):%> %(u%):%> 
    %(aa%):%> %(ar%):%> %(ar%*%):%> %(ng%):%> %(ur%):%>
    ! %(g%/t%):%> %(r%/l%):%> %(u%/i%):%>
    
    %+:%> %–:%> %~:%> %%:%> %::%> %':%> %.:%> %@:%> %*:%>
    %= ; 

Sets 
    Bndry = %+ %– %~ %% %: %' %. %@ %* %(a%) %(e%) %(g%) %(r%) %(s%) %(t%) %(u%) %(aa%) %(ar%) %(ar%*%) %(ng%) %(ur%) %(g%/t%) %(r%/l%) %(u%/i%) ;

    Stop = p t c k q ;
    VoiceFric = v l s g r u͡g u͡r ; 
    VlessFric = vv ll ss gg rr u͡gg u͡rr ;
    VoiceNasal = m n ng ;
    VlessNasal = ḿ ń ńg ;

    VoicedCon = VoiceFric VoiceNasal ;
    VlessCon = VlessFric VlessNasal Stop ;

    Fric = VoiceFric VlessFric ;
    Nasal = VoiceNasal VlessNasal ;

    Con = Stop Fric Nasal w y ;

    FVow = a i u ;
    Vow = FVow e ;

Rules 

"a:0 = --"
a:0 <=> _ Con:0 Bndry:* %–: %–: l:0 ; ! "Rule for Double Minus" ! - optional drop preceding vowel also
    except
        _ g:0 Bndry:* %(e%):i ; ! age -> ii 
        _ g:0 Bndry:* a:i ; ! aga -> ii 
        _ Bndry:* [ g:0 | %(ng%):0 ] Bndry:* a:i ; ! anga, aga -> ii
        a:i Bndry:* [ g:0 | %(ng%):0 ] Bndry:* _ ; ! anga, aga -> ii
        e:i Bndry:* %(ng%):0 Bndry:* _ ; ! enga -> ii

"a:i = age/ii | aga/ii | anga/ii | aga/ii | enga/ii"
a:i <=> _ g:0 Bndry:* %(e%):i ; ! age -> ii 
        _ g:0 Bndry:* a:i ; ! aga -> ii 
        _ Bndry:* [ g:0 | %(ng%):0 ] Bndry:* a:i ; ! anga, aga -> ii
        a:i Bndry:* [ g:0 | %(ng%):0 ] Bndry:* _ ; ! anga, aga -> ii
        e:i Bndry:* %(ng%):0 Bndry:* _ ; ! enga -> ii

"e:0 = ~ | word# | VowPostbase | (e):e"
e:0 <=> _ [ Bndry: - %': ]* %~: ; ! "Rule for Tilde"
        Vow t:n _ [ .#. | %= ] ; ! "Word final Vte -> e:0"
        _ [ Bndry: - %~: ]+ Vow ; ! "vowel postbases are e deleters"
        _ [ r | g ] Bndry:+ Vow ; ! "vowel postbases are semi-final e deleters"
        _ [ r | g ] Bndry:* %%:0 Bndry:* %(e%):e ; ! "Rule (e):e"
        _ (Con:0) Bndry:* %–: %–: l:0 ; ! "Rule for Double Minus" ! - optional drop preceding vowel also
    except
        _ Bndry:* %(ng%):0 Bndry:* a:i ; ! enga -> ii
        _ Bndry:* %(ng%):0 Bndry:* i ; ! engi -> ai

"e:a = e^a | engi/ai"
e:a <=> .#. ~[ :* Vow t:n] _ [ .#. | %= ] ; ! "Word final e:a"
        _ Bndry:* %(ng%):0 Bndry:* i ; ! engi -> ai

"e:i = enga/ii"
e:i <=> _ Bndry:* %(ng%):0 Bndry:* a:i ; ! enga -> ii

"g:0 = - | :"
g:0 <=> _ Bndry:* %–: ; ! Rule for Minus - drop consonant ending base
        [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* _ [Bndry:|:0]* %:: [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon
        [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* %:: [Bndry:|:0]* _ [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon
    except
        [r:0|rr:0] Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"g:r = Uvular/VelarAgreement"
g:r <=> [r:0|rr:0] Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"g:k = word#"
g:k <=> _ [ .#. | %= ] ; ! Word Final changes
    except
        [r:0|rr:0] Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"gg:rr = Uvular/VelarAgreement"
gg:rr <=> [r:0|rr:0] Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"i:0 = --"
i:0 <=> _ Con:0 Bndry:* %–: %–: l:0 ; ! "Rule for Double Minus" ! - optional drop preceding vowel also

"k:q = Uvular/VelarAgreement"
k:q <=> [r:0|rr:0] Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"l:0 = --" ! - optional drop preceding vowel also
l:0 => Vow:0 (Con:0) Bndry:* %–: %–: _ ; ! Rule for Double Minus - 

"ng:0 = :"
ng:0 <=> [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* _ [Bndry:|:0]* %:: [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon
         [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* %:: [Bndry:|:0]* _ [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon

"q:k = Uvular/VelarAgreement"
q:k <=> g:0 Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"r:0 = - | % | :"
r:0 <=> _ Bndry:* %–: ; ! Rule for Minus - drop consonant ending base
        FVow _ [Bndry: - %*:]* %%:%> ; ! Rule for Percent
        [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* _ [Bndry:|:0]* %:: [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon
        [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* %:: [Bndry:|:0]* _ [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon
    except
        g:0 Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"r:g = Uvular/VelarAgreement"
r:g <=> g:0 Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"r:q = word#"
r:q <=> _ (%*:) [ .#. | %= ] ; ! Word Final changes
    except
        g:0 Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"rr:0 = -"
rr:0 <=> _ Bndry:* %–: ; ! Rule for Minus - drop consonant ending base
    except
        g:0 Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"rr:gg = Uvular/VelarAgreement"
rr:gg <=> g:0 Bndry:* %–: Bndry:* _ ; ! Uvular/VelarAgreement

"t:c = tV--li/ci | te@y/c | te~s/c" ! - optional drop preceding vowel also
t:c <=> _ Vow:0 (Con:0) Bndry:* %–: %–: l:0 i ; ! Rule for Double Minus -
        _ (ʼ) e:0 Bndry:* %@: Bndry:* y:0 ; ! te@y/c
        _ (ʼ) e:0 Bndry:* s:0 ; ! te~s/c

"t:n = WordFinalVte"
t:n <=> Vow _ e:0 [ .#. | %= ] ; ! Word final Vte

"s:0 = te~s/c | (t)s/c"
s:0 <=> t:c (ʼ) e:0 Bndry:* _ ; ! te~s/c
        %(t%):c Bndry:* _ ; ! (t)s/c

"u:0 = --"
u:0 <=> _ Con:0 Bndry:* %–: %–: l:0 ; ! "Rule for Double Minus" ! - optional drop preceding vowel also

"y:0 = te@y"
y:0 <=> t:c (ʼ) e:0 Bndry:* %@: Bndry:* _ ; ! te@y/c

"y:s = Stop@y" 
y:s <=> VlessCon (ʼ) (e:0) Bndry:* %@: Bndry:* _; ! Stop@y/s
    except
        t:c (ʼ) e:0 Bndry:* %@: Bndry:* _ ; ! te@y/c

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!     ALLOMORPHS    !!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

"(e):e = VV[r/g]%e | e:0%e" ! - bases kept with percent
%(e%):e <=> FVow FVow [ r | g ] Bndry:* %%:0 Bndry:* _ ;
                  e:0 [ r | g ] Bndry:* %%:0 Bndry:* _ ;

"(e):i = age/ii"
%(e%):i <=> a:i g:0 Bndry:* %%:0 Bndry:* _ ; ! age -> ii 

"(e):FV = %/:" ! - bases kept with percent and dropped by colon
%(e%):Vy <=> [:Con|.#.] Vx [ g:0 | r:0 ] Bndry:* %%:0 Bndry:* %:: Bndry:* _ Bndry:* [:Con|.#.] ;
    where Vx in ( a i u )
          Vy in ( a i u )
    matched ;

! kaig+'(g/t)uq
! kaig>0 t   uq
"(t):t = CFinalBase" ! - consonant ending base
%(t%):t <=> Con Bndry:* _ ;
    except
        Con Bndry:* _ Bndry:* s:0 ; ! (t)s/c
!    except
!        :Vow [:%>|:0]* :Vow [:%>|:0]* _ ; ! "Rule (g):g"
!    where Tx in ( %(g%/t%) %(t%) ) ;

"(t):c = (t)s/c"
%(t%):c <=> Con Bndry:* _ Bndry:* s:0 ; ! (t)s/c

! qia+'(g/t)uq
! qia>0 g   uq
"(g):g = VVFinalBase" ! - double vowel ending base
%(g%):g <=> :Vow [:%>|:0]* :Vow [:%>|:0]* _ ;
!    where Gx in ( %(g%/t%) %(g%) ) ;

! cali+'(g/t)u:nga
! cali>0 t   u:
"(ng):ng = VFinalBase" ! - bases ending in a vowel
%(ng%):ng <=> Vow: Bndry:* _ ;
    except
        [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* _ [Bndry:|:0]* %:: [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon
        [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* %:: [Bndry:|:0]* _ [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon

"(ng):0 = :" ! - bases that get added then dropped after single vowel
%(ng%):0 <=> [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* _ [Bndry:|:0]* %:: [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon
             [:Con|.#.] [Bndry:|:0]* :FVow [Bndry:|:0]* %:: [Bndry:|:0]* _ [Bndry:|:0]* :FVow [Bndry:|:0]* [:Con|.#.] ; ! Rule for Colon

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!     OPERATOR      !!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

! "Rule for Plus" - useless
! "Rule for Period" - useless

"%:0 = percentNotUsedFlag" ! percent not used
%%:0 <=> g: Bndry:* _ ;
         e: r: Bndry:* _ ;
         r: %*: Bndry:* _ ;

"':ʼ = ApostropheRule"
%':%ʼ <=> .#. (Con) Vow Con e:0 [ Bndry: - %~: ]* _ ; ! "Rule for Apostrophe - short bases"

! aqume+'(g/t)uq
! aqum0>0 0   uq
!"vowel postbases are e deleters"

! nere+'(g/t)uq
! nerʼ>0 0   uq
! "Rule for Apostrophe" ! - gemination of short bases 

! assir-–llru[V→V][V][Intr][Ind][3Sg]
! assir–llru+'(g/t)uq
! assi0>llru>0 0   uq
! assillruuq
!"Rule for Minus" ! - drop consonant ending base

! arnar[N][Abl_Mod][Unpd][Sg]
! arnar%~mek
! arna0>0mek
! arnamek
!"Rule for Percent" ! - drop weak r ending base

! a n g y a r %% %: %(e%) t
! a n g y a 0 0  0  0     t

! a t k u g %% %: %(e%) t
! a t k u 0 0  0  u     t 

! kaig+'(g/t)uq
! kaig>0 t   uq
! "Rule (t):t" ! - consonant ending base

! ! qia+'(g/t)uq
! ! qia>0 g   uq
! "Rule (g):g" ! - double vowel ending base

! evcug[V][Intr][Ind][2Pl]=llu[Encl]:evcug+'(g/t)uci=llu
