! LANGUAGE: "esu"
! AUTHOR: "Lonny Alaskuk Strunk"

! This Central Alaskan Yup'ik FST morphological analyzer/generator and the 
! associated source code is available under the GNU Affero General Public 
! License (GNU AGPL v.3):
!
! Copyright (C) 2020 Lonny Alaskuk Strunk
!
! This program is free software: you can redistribute it and/or modify it under 
! the terms of the GNU Affero General Public License as published by the Free 
! Software Foundation, either version 3 of the License, or (at your option) any 
! later version.
!
! This program is distributed in the hope that it will be useful, but WITHOUT 
! ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS 
! FOR A PARTICULAR PURPOSE. See the GNU Affero General Public License for more 
! details.
!
! You should have received a copy of the GNU Affero General Public License along 
! with this program. If not, see https://www.gnu.org/licenses/.


define Bndry      ["[e]"|"(a)"|"(aa)"|"(ar)"|"(ar*)"|"(ur)"|"(e)"|"(g)"|"(ng)"|"(r)"|"(s)"|"(t)"|"(u)"|"(g|t)"|"(r|l)"|"(u|i)"|"+"|"–"|"~"|"%"|":"|"`"|"."|"@"|"@¹"|"@²"|"@³"|"@⁴"|"@⁵"|"@ˡ"|"@ˁ"|"@ʸ"|"*"|"°"|"^"|"<"|"$"];

define Sep        ["<"|"$"|"&"|"↞"|"↠"];

define Stop       [ p | t | c | k | q | k̲ | q̲ ];
define VoiceFric  [ v | l | s | g | r | u͡g | u͡r | g̲ | r̲ | ġ ];
define VlessFric  [ vv | ll | ss | gg | rr | u͡gg | u͡rr | g̲g̲ | r̲r̲ ];
define VoiceNasal [ m | n | ng ];
define VlessNasal [ m̄ | n̄ | n͞g ];

define VoiceCon   [ VoiceFric | VoiceNasal ];
define VlessCon   [ VlessFric | VlessNasal | Stop ];

define Fric       [ VlessFric  | VoiceFric  ];
define Nasal      [ VlessNasal | VoiceNasal ];

define Con        [ Stop | Fric | Nasal | w | y ];

define FVow       [ a | i | u | ạ | ụ ];
define Vow        [ e | FVow ];

define Lowercase "A"   -> "$" a,
                 "C"   -> "$" c,
                 "E"   -> "$" e,
                 "G"   -> "$" g,
                 "I"   -> "$" i,
                 "K"   -> "$" k,
                 "L"   -> "$" l,
                 "M"   -> "$" m,
                 "N"   -> "$" n,
                 "P"   -> "$" p,
                 "Q"   -> "$" q,
                 "R"   -> "$" r,
                 "S"   -> "$" s,
                 "T"   -> "$" t,
                 "U"   -> "$" u,
                 "V"   -> "$" v,
                 "W"   -> "$" w,
                 "Y"   -> "$" y,
                 "M̄"   -> "$" m̄,
                 "N̄"   -> "$" n̄,
                 "Vv"  -> "$" vv,
                 "Ll"  -> "$" ll,
                 "Ss"  -> "$" ss,
                 "Gg"  -> "$" gg,
                 "Rr"  -> "$" rr,
                 "Ng"  -> "$" ng,
                 "N͞g"  -> "$" n͞g,
                 "U͡g"  -> "$" u͡g,
                 "U͡r"  -> "$" u͡r,
                 "U͡gg" -> "$" u͡gg,
                 "U͡rr" -> "$" u͡rr;

define arExpansion "(ar)"  -> "ạ" r               .o.
                   "(ar*)" -> "ạ" r "*"            ; 

define MultipleForms t e Sep* "–r̲pag|@²vag" (->) l "<" "." v a g                    || Vow Sep* _    .o.
                     "–r̲pag|@²vag"  ->  "–" r̲ p a g                                                   .o.

                     "–r̲(ur)lur" -> "+" ụ r l u r                                   || g Sep* _    .o. ! acag -> acagurluq
                     "–r̲(ur)lur" -> "–" "r̲" ụ r l u r                                               .o. ! qayar/angyar

                     "+a|+aar" -> "+" a a r                                         || t e ("°") Sep* _     .o.
                     "+a|+aar" -> "+" a                                                                     .o.

                     "@⁵+cete|.vkar" -> "@⁵" "+" c e t e                            || Con Sep* _           .o.
                     "@⁵+cete|.vkar" (->) "@⁵" "+" c e t e                          || t e ("°") Sep* _     .o.
                     "@⁵+cete|.vkar" -> "." v k a r                                                          .o.

                     "+ciqe|@⁵ciiqe" -> "@⁵" c i i q e                              || t e ("°") Sep* _     .o. 
                     "+ciqe|@⁵ciiqe" -> "+" c i q e                                                          .o. 

                     t e ("°") Sep* "@+–`(g)ar(ar)te" -> g "<" "+" a r "ạ" r t e    || Vow Sep* _      .o. ! Vte -> Vg
                     t e ("°") Sep* "@+–`(g)ar(ar)te" -> "<" "+" a r "ạ" r t e      || Con Sep* _      .o. ! Cte -> C
                     t "'" e Sep* "@+–`(g)ar(ar)te" -> s "<" "'" "+" a r "ạ" r t e                     .o. ! t'e -> s'
                     "@+–`(g)ar(ar)te" -> "–" a r "ạ" r t e                         || r Sep* _        .o. ! r -> -
                     "@+–`(g)ar(ar)te" -> "+" a r "ạ" r t e                         || g Sep* _        .o. ! g -> +
                     "@+–`(g)ar(ar)te" -> "+" "`" "(g)" a r "ạ" r t e                                  .o. ! Vow -> +`(g)

                     i t e ("°") Sep* "@+(r|l)i" -> [i|e] ll "<" i                  || k Sep* _        .o. ! kite -> k[e|i]lli
                     t ("'") e ("°") Sep* "@+(r|l)i" -> l "<" i                                        .o. ! te -> li
                     e Sep* "@+(r|l)i" -> "<" l i                                                      .o. ! e -> li
                     "@+(r|l)i" -> r i                                              || FVow Sep* _     .o. ! FV -> ri
                     "@+(r|l)i" -> i                                                                   .o. ! r/g -> i

                     t ("'") e ("°") Sep* "@nga" -> s "<" "+" ng a                  || FVow Sep* _              .o. ! elite -> elisnga
                     l t ("'") e ("°") Sep* "@nga" -> ll [[e s]| i] "<" "+" ng a                                .o. ! ulte -> ullesnga | ullinga
                     s t ("'") e ("°") Sep* "@nga" -> ss [[e s]| i] "<" "+" ng a                                .o. ! caste -> cassesnga | cassinga
                     t ("'") e ("°") Sep* "@nga" -> i "<" "+" ng a                  || .#. (Con) Vow Con Sep* _ .o. ! sagte -> saginga
                     t ("'") e ("°") Sep* "@nga" -> "<" "+" ng a                                                .o. ! kitugte -> kitugnga (|| Con _ )
                     "@nga" -> "+" ng a                                                                         .o. ! TODO: How to disallow not te bases?

                     "@³%:(u)cir|@⁵cir" -> "@⁵" c i r                               || c e t e ("°") Sep* _      .o. ! kiircete -> kiircecia
                     "@³%:(u)cir|@⁵cir" -> "@³" ":" "(u)" c i r                                                  .o. ! ayuqe -> ayuqucia

                     t e "°" Sep* "@%:~(ng)ạ" -> l "<" "+" ạ                                                         .o. ! te° -> l
                     t "'" e Sep* "@%:~(ng)ạ" -> c "'" "<" ạ                                                         .o. ! t'e -> c'
                     t e Sep* "@%:~(ng)ạ" -> c "<" ạ                                                                 .o. ! te -> c
                     ng e Sep* "@%:~(ng)ạ" (->) ng "<" ạ                            || \.#. Con Vow (Con) Bndry* _   .o. ! ngenge -> nge
                     "@%:~(ng)ạ" -> ":" "~" "(ng)" ạ                                                                 .o.

                     t e "°" -> l e                                                 || _ Sep* "@–ngr(ar)"      .o. ! te° -> l
                     "@–ngr(ar)" -> "–" ng r a r                                    || _ Sep* ["–"|":"]        .o. ! not ar dropping
                     "@–ngr(ar)" -> "–" ng e r                                                                 .o. ! ar dropping with +

                     "@ˡ~+lu|@⁵na" -> "@⁵" n a                                      || "°" Sep* _                        .o.
                     "@ˡ~+lu|@⁵na" -> "@ˡ" "~" "+" l u                                                                   .o.
                     "[STE_2Sg]" -> k                                               || n a Sep* _                        .o.
                     "[STE_2Sg]" -> t e n                                                                                .o.
                     i (->) u                                                       || \[i] Sep* _ Sep* t e "°" Sep* "@⁵" n a      .o. ! (e.g nereksainani (->) nereksaunani)

                     "–lria|@⁴+ngur*" (->) "@⁴" "+" ng u r "*"                      || t e "°" Sep* _                                                     .o. ! te° (->) @ngur*
                     "–lria|@⁴+ngur*" -> "–" [l|ll] r i i                           || [Stop|VlessCon] ("'") e ("°") ([r|rr|g]) Sep* _ Sep* "%" ":" "(e)" .o. ! llrii/lrii
                     "–lria|@⁴+ngur*" -> "–" l r i i                                || _ Sep* "%" ":" "(e)"                                               .o. ! lrii
                     "–lria|@⁴+ngur*" -> "–" [l|ll] r i a                           || [Stop|VlessCon] ("'") e ("°") ([r|rr|g]) Sep* _                    .o. ! llria/lria
                     "–lria|@⁴+ngur*" -> "–" l r i a                                                                                                      .o. ! lria

                     "[Opt_PRS_S_2Sg]" -> "~" "(g)" i                               || [ Vow Vow | [ Con - t ] e ] Sep* _    .o.
                     "[Opt_PRS_S_2Sg]" -> "+" n                                     || t e Sep*  _                           .o.
                     t e "°" Sep* "[Opt_PRS_S_2Sg]" -> l "<" "&" u                                                           .o.
                     "[Opt_PRS_S_2Sg]" -> ":" a                                     || Con  Sep* _                           .o.
                     "[Opt_PRS_S_2Sg]" -> 0                                                                                  .o.
                     "[Opt_PRS_S_2PlDu]" (->) "~" "(g)" i                           || [ Vow Vow | [ Con - t ] e ] Sep* _    .o.
                     "[Opt_PRS_S_2PlDu]" -> "@⁵" "+"                                                                         .o.
                     "[Opt_PRS_A_2Sg]" (->) "~" "(g)" i                             || [ Vow Vow | [ Con - t ] e ] Sep* _    .o.
                     "[Opt_PRS_A_2Sg]" -> "@⁴" "+"                                                                           .o.
                     "[Opt_PRS_A_2Sg_P_3Sg]" -> "~" "(g)" i "↠" u                   || [ Vow Vow | [ Con - t ] e ] Sep* _    .o.
                     t e Sep* "[Opt_PRS_A_2Sg_P_3Sg]" -> s "<" "&" "↠" g u                                                   .o.
                     t e "°" Sep* "[Opt_PRS_A_2Sg_P_3Sg]" -> l "<" "&" "↠" g u                                               .o.
                     "[Opt_PRS_A_2Sg_P_3Sg]" -> "↠" "–" gg u                        || g Sep* _                              .o.
                     "[Opt_PRS_A_2Sg_P_3Sg]" -> "↠" "–" rr u                        || r Sep* _                              .o.
                     "[Opt_PRS_A_2Sg_P_3Sg]" -> "↠" u                                                                        .o.
                     "[Opt_PRS_A_2Sg_P_1Sg]" (->) "~" "(g)" i "↠" a                 || [ Vow Vow | [ Con - t ] e ] Sep* _    .o.
                     "[Opt_PRS_A_2Sg_P_1Sg]" -> "↠" "@⁴" "+" ng a                                                            .o.

                     "@⁵+p|~v" -> "@⁵" "+" p                                        || [ t e ("°") | Con ] Sep* _   .o.
                     "@⁵+p|~v" -> "~" v                                                                             .o.
                     "+p|.v" -> "+" p e                                             || Con Sep* _ Sep* Con          .o.
                     "+p|.v" -> "+" p                                               || Con Sep* _                   .o.
                     "+p|.v" -> "." v                                                                               .o.
                     "+pegun|.vkun" -> "+" p e "&" g u n                            || Con Sep* _                   .o.
                     "+pegun|.vkun" -> "." v "&" k u n                                                              .o.
                     "+t|.s" -> "+" t                                               || Con Sep*  _                  .o.
                     "+t|.s" -> "." s                                                                               .o.
                     "+c|.s" -> "+" c                                               || Con Sep* _                   .o.
                     "+c|.s" -> "." s                                                                               .o.
                     "+c|.ss" -> "." ss                                             || Vow Sep* _                   .o.
                     "+c|.ss" -> "+" c                                                                              .o.          
                     "('a)" -> ' a                                                  || \.#. Con a Sep* _            .o.
                     "('a)" -> 0                                                                                     ;


define AutomaticDevoicing v -> vv,
                          l -> ll,
                          s -> ss,
                          g -> gg,
                          g̲ -> g̲g̲,
                          r -> rr,
                          r̲ -> r̲r̲,
                          u͡g -> u͡gg,
                          u͡r -> u͡rr         || [ Stop | VlessFric ] Sep* _      ,
                                               _ Sep* [ Stop ]                 .o.
                          m -> m̄,
                          n -> n̄,
                          ng -> n͞g          || [ Stop | VlessFric ] Sep* _        ;

! Unnecessary devoicing                           
!                          r -> rr          || _ Sep* .#.                            .o.
!                          s -> ss          || .#. Sep* _                             ;

regex 
      Lowercase          .o. ! comment out to pass through for [NonYupik]
      arExpansion        .o.
      MultipleForms      .o.  
      AutomaticDevoicing  ;
