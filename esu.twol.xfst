! LANGUAGE: "esu"
! AUTHOR: "Lonny Strunk"

! TODO:
!   - from -rraar-: terr -> q ; e.g. inarterraarluni -> inarqaarluni


define Stop               [ p | t | c | k | q ];
define VoicedFricative    [ v | l | s | g | r | u͡g | u͡r ];
define VoicelessFricative [ vv | ll | ss | gg | rr | u͡gg | u͡rr ];
define VoicedNasal        [ m | n | ng ];
define VoicelessNasal     [ ḿ | ń | ńg ];

define VoicedC    [ VoicedFricative | VoicedNasal ];
define VoicelessC [ VoicelessFricative | VoicelessNasal | Stop ];

define Fricative  [ VoicelessFricative | VoicedFricative ];
define Nasal      [ VoicelessNasal     | VoicedNasal     ];

define Con        [ Stop | Fricative | Nasal | w | y ];

define FVow       [ a | i | u ];
define Vow        [ e | FVow ];

define AutomaticDevoicing v -> vv,
                          l -> ll,
                          s -> ss,
                          g -> gg,
                          r -> rr,
                          u͡g -> u͡gg,
                          u͡r -> u͡rr || [ Stop | VoicelessFricative ] ">"* _  ,
                                     _ ">"* [ Stop ]                        .o.
                          m -> ḿ,
                          n -> ń,
                          ng -> ńg || [ Stop | VoicelessFricative ] ">"* _   ;
!                          r -> rr  || _ ">"* .#.                            .o.
!                          s -> ss  || .#. ">"* _                             ;

define WeakInitialE [..] -> "ʼ" || "[e]" [Con]+ [Vow]+ ">"+ Con _ ">"* FVow  .o.
                    [..] -> "ʼ" || "[e]" [Con]+ [Vow]+ ">"* Con _ ">"+ FVow  .o.
                    "[e]" -> 0  || _ [Con|Vow]+ ">"                          .o. 
                    "[e]" -> e                                                ;

define DropHattedE e -> 0 || .#. ">"* [Vow ">"* Con ">"* Vow ">"* Con ">"*]* Vow ">"* Con ">"* _ ">"* Con ">"* Vow                   ,  ! #(VCVC)*VCeCV
                             [Con|.#.] ">"* Con ">"* [Vow ">"* Con ">"* Vow ">"* Con ">"*]* Vow ">"* Con ">"* _ ">"* Con ">"* Vow   .o. ! [#/C]C(VCVC)*VCeCV
                [..] -> e || p _ ">"* p , t _ ">"* t , c _ ">"* c , k _ ">"* k , q _ ">"* q , 
                             v _ ">"* v , l _ ">"* l , s _ ">"* s , g _ ">"* g , r _ ">"* r , u͡g _ ">"* u͡g , u͡r _ ">"* u͡r ,
                             vv _ ">"* vv , ll _ ">"* ll , ss _ ">"* ss, gg _ ">"* gg , rr _ ">"* rr , u͡gg _ ">"* u͡gg , u͡rr _ ">"* u͡rr ,
                             m _ ">"* m , n _ ">"* n , ng _ ">"* ng , ḿ _ ">"* ḿ , ń _ ">"* ń , ńg _ ">"* ńg , 
                             w _ ">"* w , y _ ">"* y , c _ ">"* t , t _ ">"* c , c _ ">"* n , n _ ">"* c                               ;

define TripleConsonant [..]  ->  e || Con _ ">"* ss ">"* Con   .o.
                       [..]  ->  e || Con ">"* t _ ">"* Con   .o.
                       [..] (->) e || Con ">"* Con _ ">"* Con .o.
                       [..]  ->  e || Con _ ">"* Con ">"* Con  ;

define RemoveGeminatedApostrophe "ʼ" -> 0 || [ .#. | Con ] ">"* Vow ">"* Con ">"* _ ">"* Vow ">"* Vow ;

define ToOrthography [..] -> "ʼ" || Stop _ ">"* VoicedC , n _ ">"* g          .o.
                      vv -> v,
                      ll -> l,
                      ss -> s,
                      gg -> g,
                      rr -> r,
                      u͡gg -> u͡g,
                      u͡rr -> u͡r || [ Stop | VoicelessFricative ] ">"* _  ,
                                 _ ">"* [ Stop ]                        .o.
                      ḿ -> m,
                      ń -> n,
                      ńg -> ng || [ Stop | VoicelessFricative ] ">"* _  .o.
                      rr -> r  || _ ">"* .#.                            .o.
                      ss -> s  || .#. ">"*  _                            ;
                    

regex AutomaticDevoicing        .o.
      WeakInitialE              .o. 
      DropHattedE               .o.
      TripleConsonant           .o.
      RemoveGeminatedApostrophe .o.
      ToOrthography              ;
