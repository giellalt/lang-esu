! LANGUAGE: "esu"
! AUTHOR: "Lonny Strunk"

! TODO:
!   - from -rraar-: terr -> q ; e.g. inarterraarluni -> inarqaarluni


define Stop       [ p | t | c | k | q ];
define VoiceFric  [ v | l | s | g | r | u͡g | u͡r ];
define VlessFric  [ vv | ll | ss | gg | rr | u͡gg | u͡rr ];
define VoiceNasal [ m | n | ng ];
define VlessNasal [ ḿ | ń | ńg ];

define VoiceCon   [ VoiceFric | VoiceNasal ];
define VlessCon   [ VlessFric | VlessNasal | Stop ];

define Fric       [ VlessFric  | VoiceFric  ];
define Nasal      [ VlessNasal | VoiceNasal ];

define Con        [ Stop | Fric | Nasal | w | y ];

define FVow       [ a | i | u ];
define Vow        [ e | FVow ];

define AutomaticDevoicing v -> vv,
                          l -> ll,
                          s -> ss,
                          g -> gg,
                          r -> rr,
                          u͡g -> u͡gg,
                          u͡r -> u͡rr || [ Stop | VlessFric ] ">"* _  ,
                                     _ ">"* [ Stop ]                        .o.
                          m -> ḿ,
                          n -> ń,
                          ng -> ńg || [ Stop | VlessFric ] ">"* _   ;

! Unnecessary devoicing                          
!                          r -> rr  || _ ">"* .#.                            .o.
!                          s -> ss  || .#. ">"* _                             ;

define WeakInitialE [..] -> "ʼ" || "[e]" [Con]+ [Vow]+ ">"+ Con _ ">"* FVow  .o.
                    [..] -> "ʼ" || "[e]" [Con]+ [Vow]+ ">"* Con _ ">"+ FVow  .o.
                    "[e]" -> 0  || _ [Con|Vow]+ ">"                          .o. 
                    "[e]" -> e                                                ;

define DropHattedE e -> 0 || .#. [Vow ">"* Con ">"* Vow ">"* Con ">"*]* Vow ">"* Con ">"* _ ">"* Con ">"* Vow                        ,  ! #(VCVC)*VCeCV
                             [Con|.#.] ">"* Con ">"* [Vow ">"* Con ">"* Vow ">"* Con ">"*]* Vow ">"* Con ">"* _ ">"* Con ">"* Vow   .o. ! [#/C]C(VCVC)*VCeCV
                [..] -> e || p _ ">"* p , t _ ">"* t , c _ ">"* c , k _ ">"* k , q _ ">"* q , 
                             v _ ">"* v , l _ ">"* l , s _ ">"* s , g _ ">"* g , r _ ">"* r , u͡g _ ">"* u͡g , u͡r _ ">"* u͡r ,
                             vv _ ">"* vv , ll _ ">"* ll , ss _ ">"* ss, gg _ ">"* gg , rr _ ">"* rr , u͡gg _ ">"* u͡gg , u͡rr _ ">"* u͡rr ,
                             m _ ">"* m , n _ ">"* n , ng _ ">"* ng , ḿ _ ">"* ḿ , ń _ ">"* ń , ńg _ ">"* ńg , 
                             w _ ">"* w , y _ ">"* y , c _ ">"* t , t _ ">"* c                                                       ; ! maybe c _ ">"* n , n _ ">"* c ??

define TripleConsonant  rr   ->  0 || _ q ">"* Con                .o.
                       [..]  ->  e || Con _ ">"* [s|ss] ">"* Con  .o.
                       [..]  ->  e || Con ">"* t _ ">"* Con       .o.
                       [..] (->) e || Con ">"* Con _ ">"* Con     .o.
                       [..]  ->  e || Con _ ">"* Con ">"* Con      ;

define TripleVowel FVow -> 0 || FVow ">"* _ ">"* FVow ;

define TetToCet t (->) c || _ e ">"* t ,
                            _ e ">"* ss ">"* c ; ! kitugtesciigataqa -> kitugcesciigataqa

define RemoveApostrophe "ʼ" -> 0 || [ .#. | Con ] ">"* Vow ">"* Con ">"* _ ">"* Vow ">"* Vow ,
                                    VoiceCon ">"*  _ ">"* VoiceCon                           ;

define ToOrthography [..] -> "ʼ" || VlessCon _ ">"* VoiceCon , VoiceFric _ ">"* VlessCon , n _ ">"* g   .o.
                      vv -> v,
                      ll -> l,
                      ss -> s,
                      gg -> g,
                      rr -> r,
                      u͡gg -> u͡g,
                      u͡rr -> u͡r || [ Stop | VlessFric ] ">"* _           ,
                                 _ ">"* [ Stop ]                        .o.
                      ḿ -> m,
                      ń -> n,
                      ńg -> ng || [ Stop | VlessFric ] ">"* _           .o.
                      rr -> r  || _ ">"* .#.                            .o.
                      ss -> s  || .#. ">"*  _                            ;
                    

regex AutomaticDevoicing   .o.
      WeakInitialE         .o. 
      DropHattedE          .o.
      TripleConsonant      .o.
      TripleVowel          .o.
      TetToCet             .o.
      RemoveApostrophe     .o.
      ToOrthography         ;
