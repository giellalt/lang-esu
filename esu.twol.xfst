! LANGUAGE: "esu"
! AUTHOR: "Lonny Strunk"

!! NOTE !!!
! 1. Morpheme Operators are indicated by ">"
! 2. Vowels may be hatted or stressed (i.e. a -> [â|á])

! TODO:
!   - from -rraar-: terr -> q ; e.g. inarterraarluni -> inarqaarluni

define Stop       [ p | t | c | k | q ];
define VoiceFric  [ v | l | s | g | r | u͡g | u͡r ];
define VlessFric  [ vv | ll | ss | gg | rr | u͡gg | u͡rr ];
define VoiceNasal [ m | n | ng ];
define VlessNasal [ ḿ | ń | ńg ];

define VoiceCon   [ VoiceFric | VoiceNasal ];
define VlessCon   [ VlessFric | VlessNasal | Stop ];

define Fric       [ VlessFric  | VoiceFric  ];
define Nasal      [ VlessNasal | VoiceNasal ];

define Con        [ Stop | Fric | Nasal | w | y ];

define FVow       [ a | i | u | ạ ];
define FVowStress [ á | í | ú | ạ́ ];
define FVowHatted [ â | î | û | ậ ];
define AllFVow    [ FVow | FVowStress | FVowHatted ];
define Vow        [ FVow | e ];
define VowStress  [ FVowStress | é ];
define VowHatted  [ FVowHatted | ê ];
define AllVow     [ Vow | VowStress | VowHatted ];

define AutomaticDevoicing v -> vv,
                          l -> ll,
                          s -> ss,
                          g -> gg,
                          r -> rr,
                          u͡g -> u͡gg,
                          u͡r -> u͡rr || [ Stop | VlessFric ] ">"* _  ,
                                     _ ">"* [ Stop | VlessFric ]             .o.
                          m -> ḿ,
                          n -> ń,
                          ng -> ńg || [ Stop | VlessFric ] ">"* _   ;

! Unnecessary devoicing                          
!                          r -> rr  || _ ">"* .#.                            .o.
!                          s -> ss  || .#. ">"* _                             ;

define WeakInitialE [..] -> "ʼ" || "[e]" [Con]+ [AllVow]+ ">"+ Con _ ">"* AllFVow  .o.
                    [..] -> "ʼ" || "[e]" [Con]+ [AllVow]+ ">"* Con _ ">"+ AllFVow  .o.
                    "[e]" -> 0  || _ [Con|AllVow]+ ">"                          .o. 
                    "[e]" -> e                                                ;

define DropHattedE ê -> 0 .o.
                [..] -> e || p _ ">"* p , t _ ">"* t , c _ ">"* c , k _ ">"* k , q _ ">"* q , 
                             v _ ">"* v , l _ ">"* l , s _ ">"* s , g _ ">"* g , r _ ">"* r , u͡g _ ">"* u͡g , u͡r _ ">"* u͡r ,
                             vv _ ">"* vv , ll _ ">"* ll , ss _ ">"* ss, gg _ ">"* gg , rr _ ">"* rr , u͡gg _ ">"* u͡gg , u͡rr _ ">"* u͡rr ,
                             m _ ">"* m , n _ ">"* n , ng _ ">"* ng , ḿ _ ">"* ḿ , ń _ ">"* ń , ńg _ ">"* ńg , 
                             w _ ">"* w , y _ ">"* y , c _ ">"* t , t _ ">"* c                                                       ; ! maybe c _ ">"* n , n _ ">"* c ??

define TripleConsonant  rr   ->  0 || _ q ">"* Con                .o. ! rqe- bases drop r
                       [..]  ->  e || Con _ ">"* [s|ss] ">"* Con  .o.
                       [..]  ->  e || Con ">"* t _ ">"* Con       .o.
                       [..] (->) e || Con ">"* Con _ ">"* Con     .o.
                       [..]  ->  e || Con _ ">"* Con ">"* Con      ;

define TripleVowel AllFVow -> 0 || AllFVow ">"* _ ">"* AllFVow ;

define WordAdjustments t (->) c     || _ [e|é] ">"* t ,               ! angutet -> angucet
                                       _ [e|é] ">"* ss ">"* c     .o. ! kitugtesciigataqa -> kitugcesciigataqa
                       [a|â|á] -> e || ">" q _ ">"* [r|rr]         ;  ! apteqarru -> apteqerru for -qar- postbase


define arDeletion  "ạ" q -> 0 || _ ">"* .#.                                             .o. ! wordFinal
                   ["ạ"|"ạ́"] [r|rr] -> 0 || Vow ">"* VowStress ">"* Con ">"* _ ">"* Con .o. ! VV́r(ar) -> VV́r
                   "ạ" [r|rr] -> 0 || Con ">"* VowStress ">"* Con ">"* _ ">"* Con       .o. ! CV́r(ar) -> CV́r
                   "â" -> a "ʼ" á,
                   "î" -> i "ʼ" í,
                   "û" -> u "ʼ" ú  || _ ">"* Con ">"* ["ạ"|"ạ́"] [r|rr] ">"* Con         .o. ! V̂r(ar) -> V'Vr
                   ["ạ"|"ạ́"] [r|rr] -> 0 || Vow "ʼ" VowStress ">"* Con ">"* _ ">"* Con  .o. ! V̂r(ar) -> V'Vr
                   a -> á "ʼ",
                   e -> é "ʼ",
                   i -> í "ʼ",
                   u -> ú "ʼ" || _ ">"* Con ">"* "ạ́" [r|rr] ">"* Con                     .o. ! Vr(ár) -> V́(ʼ)r
                   "ạ́" [r|rr] -> 0 || VowStress "ʼ" ">"* Con ">"* _ ">"* Con             .o. ! Vr(ár) -> V́(ʼ)r
                   "ʼ" (->) 0 || VowStress _ ">"* Con                                     ;  ! Vr(ár) -> V́(ʼ)r


define RemoveApostrophe "ʼ" -> 0 || [ .#. | Con ] ">"* AllVow ">"* Con ">"* _ ">"* AllVow ">"* AllVow ,
                                    VoiceCon ">"*  _ ">"* VoiceCon                           ;

define AddApostrophe [..] -> "ʼ" || VlessCon _ ">"* VoiceCon , n _ ">"* g   ;      ! maybe VoiceFric _ ">"* VlessCon  ???

define ToOrthography  vv -> v,
                      ll -> l,
                      ss -> s,
                      gg -> g,
                      rr -> r,
                      u͡gg -> u͡g,
                      u͡rr -> u͡r || [ Stop | VlessFric ] ">"* _           ,
                                 _ ">"* [ Stop ]                        .o.
                      ḿ -> m,
                      ń -> n,
                      ńg -> ng || [ Stop | VlessFric ] ">"* _           .o.
                      rr -> r  || _ ">"* .#.                            .o.
                      ss -> s  || .#. ">"*  _                           .o.
                      [â|á|ạ|ậ|ạ́] -> a                                  .o.
                      [ê|é] -> e                                        .o.
                      [î|í] -> i                                        .o.
                      [û|ú] -> u                                         ;
                    

regex AutomaticDevoicing   .o.
      WeakInitialE         .o. 
      DropHattedE          .o.
      TripleConsonant      .o.
      TripleVowel          .o.
      WordAdjustments      .o.
      arDeletion           .o.
      RemoveApostrophe     .o.
      AddApostrophe        .o.
      AutomaticDevoicing   .o.
      ToOrthography         ;
